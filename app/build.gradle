apply plugin: 'com.android.application'

android {

    def keyProps = new Properties()
    def keyPropsFile = rootProject.file('secret')

    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    signingConfigs {
        // load from secret file for not sharing on git

        if (keyPropsFile.exists()) {
            keyProps.load(new FileInputStream(keyPropsFile))

            clipster_key {
                keyAlias keyProps['keyAlias']
                keyPassword keyProps['keyPassword']
                storeFile file(keyProps['storeFile'])
                storePassword keyProps['storePassword']
            }
        }

    }

    defaultConfig {
        applicationId "com.data_dive.com.clipster"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 4
        versionName "0.2.2"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (keyPropsFile.exists()) {
                signingConfig signingConfigs.clipster_key
            }
        }
    }

    // Needed for fernet-java8, otherwise strange untrackable behaviour
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

repositories {
    flatDir {
        dirs'libs'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment:2.3.2'
    implementation 'androidx.navigation:navigation-ui:2.3.2'
    implementation 'com.macasaet.fernet:fernet-java8:1.5.0'
    implementation 'com.amdelamar:jhash:2.2.0'
}